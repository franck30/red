apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-perf'

android {
  compileSdkVersion rootProject.config.compileSdkVersion
  buildToolsVersion rootProject.config.buildToolsVersion
  defaultConfig {
    applicationId rootProject.config.applicationId
    minSdkVersion rootProject.config.minSdkVersion
    targetSdkVersion rootProject.config.targetSdkVersion
    versionCode Integer.parseInt(System.getenv('CIRCLE_BUILD_NUM') ?: "153")
    versionName rootProject.config.versionName
    testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    vectorDrawables {
      useSupportLibrary true
    }
  }

  signingConfigs {
    release {
      storeFile rootProject.file(System.getenv('KEYSTORE') ?: KEYSTORE)
      storePassword System.getenv('KEYSTORE_PASSWORD') ?: KEYSTORE_PASSWORD
      keyAlias System.getenv('KEY_ALIAS') ?: KEY_ALIAS
      keyPassword System.getenv('KEY_PASSWORD') ?: KEY_PASSWORD
    }
  }

  buildTypes {
    release {
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
      minifyEnabled false
    }
  }

  dataBinding {
    enabled true
  }

  compileOptions {
    targetCompatibility 1.8
    sourceCompatibility 1.8
  }

  dexOptions {
    preDexLibraries = Boolean.valueOf(System.getProperty('pre-dex', 'true'))
  }

  flavorDimensions 'foot'
  productFlavors {
    prod {}
  }

  // Always show the result of every unit test, even if it passes.
  testOptions {
    unitTests {
      all {
        testLogging {
          events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
      }
      returnDefaultValues = true
      includeAndroidResources = true
    }
  }

  lintOptions {
    warning 'InvalidPackage' // prevent error from references of non-Android package
  }

  sourceSets {
    androidTest.java.srcDirs += 'src/androidTest/kotlin'
  }
}

androidExtensions {
  experimental = true
}

android.applicationVariants.all {
  // Set this to false to disable Firebase Performance Monitoring at compile time
  FirebasePerformance {
    instrumentationEnabled true
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
  implementation "android.arch.lifecycle:extensions:$rootProject.lib.archComponentsVersion"
  kapt "android.arch.lifecycle:compiler:$rootProject.lib.archComponentsVersion"
  implementation "com.android.support:appcompat-v7:$rootProject.lib.supportLibraryVersion"
  implementation "com.android.support:support-annotations:$rootProject.lib.supportLibraryVersion"
  implementation "com.android.support:design:$rootProject.lib.supportLibraryVersion"
  implementation "com.android.support:recyclerview-v7:$rootProject.lib.supportLibraryVersion"
  implementation "com.android.support:design:$rootProject.lib.supportLibraryVersion"
  implementation "com.android.support:support-v4:$rootProject.lib.supportLibraryVersion"
  implementation "com.android.support:cardview-v7:$rootProject.lib.supportLibraryVersion"
  implementation "com.android.support.constraint:constraint-layout:$rootProject.lib.constraintLayoutVersion"
  implementation "com.jakewharton.timber:timber:$rootProject.lib.timberVersion"
  implementation "io.reactivex.rxjava2:rxjava:$rootProject.lib.rxJavaVersion"
  implementation "io.reactivex.rxjava2:rxkotlin:$rootProject.lib.rxKotlinVersion"
  implementation "io.reactivex.rxjava2:rxandroid:$rootProject.lib.rxAndroidVersion"
  implementation "com.squareup.picasso:picasso:$rootProject.lib.picassoVersion"
  implementation "com.jakewharton.picasso:picasso2-okhttp3-downloader:$rootProject.lib.picassoOkHttp3DownloaderVersion"
  implementation "com.squareup.okhttp3:okhttp:$rootProject.lib.okHttpVersion"
  implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.lib.okHttpVersion"
  implementation "com.squareup.retrofit2:retrofit:$rootProject.lib.retrofitVersion"
  implementation "com.squareup.retrofit2:converter-moshi:$rootProject.lib.retrofitVersion"
  implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.lib.retrofitVersion"
  implementation "com.squareup.moshi:moshi:$rootProject.lib.moshiVersion"
  implementation "com.squareup.moshi:moshi-adapters:$rootProject.lib.moshiVersion"
  implementation "com.google.firebase:firebase-core:$rootProject.lib.firebaseVersion"
  implementation "com.google.firebase:firebase-perf:$rootProject.lib.firebaseVersion"
  // Only use crash reporting in the "prod" product flavor
  prodImplementation "com.google.firebase:firebase-crash:$rootProject.lib.firebaseVersion"
  implementation "com.jakewharton.rxbinding2:rxbinding:$rootProject.lib.rxBindingVersion"
  implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:$rootProject.lib.rxBindingVersion"
  implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:$rootProject.lib.rxBindingVersion"
  implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:$rootProject.lib.rxBindingVersion"
  implementation "com.benoitquenaudon:rxdatabinding:$rootProject.lib.rxDataBindingVersion"
  debugImplementation "com.squareup.leakcanary:leakcanary-android:$rootProject.lib.leakCanaryVersion"
  releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.lib.leakCanaryVersion"
  kapt "com.android.databinding:compiler:$android_plugin_version"
  implementation "com.google.dagger:dagger-android:$rootProject.lib.daggerVersion"
  implementation "com.google.dagger:dagger-android-support:$rootProject.lib.daggerVersion"
  implementation "com.google.dagger:dagger:$rootProject.lib.daggerVersion"
  kapt "com.google.dagger:dagger-android-processor:$rootProject.lib.daggerVersion"
  kapt "com.google.dagger:dagger-compiler:$rootProject.lib.daggerVersion"
  implementation "se.ansman.kotshi:api:$rootProject.lib.kotshiVersion"
  kapt "se.ansman.kotshi:compiler:$rootProject.lib.kotshiVersion"
  implementation "androidx.core:core-ktx:$rootProject.lib.androidxVersion"

  // JVM Tests
  testImplementation "junit:junit:$rootProject.lib.junitVersion"
  testImplementation "com.squareup.okhttp3:mockwebserver:$rootProject.lib.okHttpVersion"
  testImplementation "org.mockito:mockito-core:$rootProject.lib.mockitoVersion"
  testImplementation "com.squareup.retrofit2:retrofit-mock:$rootProject.lib.retrofitVersion"
  kaptTest "com.google.dagger:dagger-compiler:$rootProject.lib.daggerVersion"

  androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$rootProject.lib.espressoVersion"
  androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.lib.espressoVersion"
  kaptAndroidTest "com.google.dagger:dagger-compiler:$rootProject.lib.daggerVersion"
}

/*
Resolves dependency versions across test and production APKs, specifically, transitive
dependencies. This is required since Espresso internally has a dependency on support-annotations.
*/
configurations.all {
  resolutionStrategy.force "com.android.support:support-annotations:$rootProject.lib.supportLibraryVersion"
  resolutionStrategy.force "com.google.code.findbugs:jsr305:$rootProject.lib.jsr305Version"
}

apply plugin: 'com.google.gms.google-services'
